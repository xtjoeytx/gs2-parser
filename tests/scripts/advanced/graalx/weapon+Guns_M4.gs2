//#CLIENTSIDE
function onCreated()
{
  this.badanis = {"grab", "pull", "push", "swim"};
}

function onWeaponFired()
{
  this.muditem = new TStaticVar();
  this.muditem.loadvarsfromarray(makevar("clientr.muditem_" @ client.selectedweapon));
  if (this.muditem.itemtype == "ARCHETYPE_GUN" && this.muditem.weapon == name)
  {
    if (this.gunout <= 0)
    {
      onEquip();
    }
    else
    {
      if (this.burst != 2)
      {
        onFire();
      }
    }
    onTimeout();
  }
}

function onTimeout()
{
  if (this.gunout == client.selectedweapon)
  {
    this.muditem = new TStaticVar();
    this.muditem.loadvarsfromarray(makevar("clientr.muditem_" @ client.selectedweapon));
    client.ammo = this.muditem.loaded - client.ammosave;
    
    if (this.changed == true && (this.muditem.loaded - client.ammosave) != this.oldvalue)
    {
      this.changed = false;
    }
    
    if (this.shot <= 0)
    {
      if (player.weapon != "+Guns/M4" || player.ani == "swim")
      {
        onUnequip();
      }
      
      if (keydown(4))
      {
        if (this.burst == 2)
        {
          onFire();
        }
      }
      
      if (keydown(5))
      {
        onUnequip();
      }
      
      if (keydown(6))
      {
        onReload();
      }
    }
  }
  elseif (this.gunout != -1)
    onUnequip();
    
  if (this.shot > 0) this.shot -= 0.05;
  setTimer(0.05);
}

function onKeyPressed(keycode, keychar)
{
  if (this.gunout > 0)
  {
    if (keychar == "F" && playerfreeze <= 0)
    {
      if (this.gout == true)
      {
        this.savedir = {strafedir, player.dir};
        strafedir = -1;
        player.dir = 2;
        setani("gx_m4-case-disassemble", NULL);
        playerfreeze = 5.4;
        findWeapon("-Movement").changeAnis("gx_m4-case-idle", "gx_m4-case-walk", "swim", "push", "pull", "grab", "sit", "sleep", "map");
        sleep(5.4);
        setani("gx_m4-case-idle", NULL);
        strafedir = this.savedir[0];
        player.dir = this.savedir[1];
        this.gout = false;
      }
      else
      {
        this.savedir = {strafedir, player.dir};
        strafedir = -1;
        player.dir = 2;
        setani("gx_m4-case-assemble", NULL);
        playerfreeze = 5.4;
        findWeapon("-Movement").changeAnis(this.muditem.gun[0] @ "-idle", this.muditem.gun[0] @ "-walk", "swim", "push", "pull", "grab", "sit", "sleep", "map");
        sleep(5.4);
        setani(this.muditem.gun[0] @ "-idle", NULL);
        strafedir = this.savedir[0];
        player.dir = this.savedir[1];
        this.gout = true;
      }
    }
    
    if (keychar == "G")
    {
      onChgBurst();
    }
  }
}

function onChgBurst()
{
  this.burst++;
  while (this.burstmodes[this.burst] == 0)
  {
    if (this.burst == this.burstmodes.size())
    {
      this.burst = 0;
    }
    else
    {
      this.burst++;
    }
  }
}

function onEquip()
{
  this.gunout = client.selectedweapon;
  this.burstmodes = this.muditem.gun[3];
  if (this.gout == true)
  {
    setani(this.muditem.gun[0] @ "-idle", NULL);
    findWeapon("-Movement").changeAnis(this.muditem.gun[0] @ "-idle", this.muditem.gun[0] @ "-walk", "swim", "push", "pull", "grab", "sit", "sleep", "map");
  }
  else
  {
    setani("gx_m4-case-idle", NULL);
    findWeapon("-Movement").changeAnis("gx_m4-case-idle", "gx_m4-case-walk", "swim", "push", "pull", "grab", "sit", "sleep", "map");
  }
  onChgBurst();
  this.shot = 0.25;
  if (this.muditem.loaded - client.ammosave > this.muditem.gun[2])
  {
    onAddUp();
  }
  client.ammo = this.muditem.loaded - client.ammosave;
  client.ammomax = this.muditem.gun[2];
  onTimeout();
}

function onFire()
{
  if (this.gout == false)
    return false;
  if (this.muditem.loaded - client.ammosave > 0)
  {
    if (this.shot <= 0)
    {
      if (this.burst == 0 || this.burst == 2)
      {
        this.maxbullets = 1;
      }
      else
      {
        if (this.burst == 1)
        {
          this.maxbullets = 3;
          if (this.muditem.loaded - client.ammosave < this.maxbullets)
            this.maxbullets = this.muditem.loaded- client.ammosave;
        }
      }
      
      // Actual Gun Firing
      if (strafedir > -1) player.dir = strafedir;
      playerfreeze = this.muditem.sfreeze[this.burst][0];
      this.shot = this.muditem.sfreeze[this.burst][1];
      setani(this.muditem.gun[0] @ "-fire", NULL);
      for (temp.i = 0; temp.i < this.maxbullets; temp.i++)
      {
        client.ammosave++;
        this.angle = getangle(vecx(player.dir), vecy(player.dir)) + random(this.muditem.gunrand[this.burst][0], this.muditem.gunrand[this.burst][1]);
        if (player.guild != NULL)
        {
          setshootparams(player.account, this.muditem.damage[this.burst], player.guild, client.nopk, "gun", this.angle);
        }
        else
        {
          setshootparams(player.account, this.muditem.damage[this.burst], "(none)", client.nopk, "gun", this.angle);
        }
        shoot(player.x + this.muditem.gundir[player.dir][0], player.y + this.muditem.gundir[player.dir][1], 0, this.angle, 1, -1, "gx_bullets", NULL);
        sleep(0.01);
      }
      // Above
    }
  }
  else
  {
    onNoLoad();
  }
}

function onNoLoad()
{
  playerfreeze = this.muditem.freeze[2];
  setani(this.muditem.gun[0] @ "-noload", NULL);
  sleep(this.muditem.freeze[3]);
}

function onReload()
{
  if (this.gout == false)
    return false;
    
  if (!contains(this.badanis, player.ani))
  {
    if (this.shot <= 0 && this.changed == false)
    {
      if (this.muditem.loaded - client.ammosave < this.muditem.gun[2])
      {
        this.diff = this.muditem.gun[2] - (this.muditem.loaded - client.ammosave);
        if (makevar("clientr.ammo_" @ this.muditem.gun[1]) <= this.diff)
        {
          this.adiff = this.diff;
          this.adiff -= makevar("clientr.ammo_" @ this.muditem.gun[1]);
          this.diff = this.muditem.gun[2] - this.adiff;
        }
        
        if (this.diff > 0 && makevar("clientr.ammo_" @ this.muditem.gun[1]) >= this.diff)
        {
          playerfreeze = this.muditem.freeze[0];
          this.shot = this.muditem.freeze[1];
          setani(this.muditem.gun[0] @ "-reload", NULL);
          this.newammo = makevar("clientr.ammo_" @ this.muditem.gun[1]) - this.diff;
          this.oldvalue = this.muditem.loaded - client.ammosave;
          this.changed = true;
          triggerserver("gui", name, "globalammo", client.selectedweapon, this.muditem.gun[1], this.newammo, this.diff, client.ammosave);
          client.ammosave = 0;
        }
      }
    }
  }
}

function onUnequip()
{
  triggerserver("gui", name, "loseammo", this.gunout, client.ammosave); 
  client.ammosave = 0;
  this.burstmodes = -1;
  this.burst = -1;
  this.gunout = -1;
  if (player.ani != "swim") setani("idle", NULL);
  play("nextpage.wav");
  findWeapon("-Movement").changeAnis3();
  playerfreeze = 0.1;
  setTimer(0);
}

function onAddUp()
{
  this.diff = (this.muditem.loaded - client.ammosave) - this.muditem.gun[2];
  this.holder = makevar("clientr.ammo_" @ this.muditem.gun[1]);
  this.newammo = makevar("clientr.ammo_" @ this.muditem.gun[1]) += this.diff;
  triggerserver("gui", name, "fixammo", client.selectedweapon, this.muditem.gun[1], this.newammo);
}