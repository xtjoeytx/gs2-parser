//#CLIENTSIDE
function onWeaponFired()
{
  if (player.level.starts("g_event-") && serverr.event_grenade == false)
  {
    return false;
  }
  
  if (this.nadeout == false)
  {
    if (player.Mud_FindWeapon(client.selectedweapon, "ARCHETYPE_ITEM", this.name))
    {
      if (this.ttick <= 0)
      {
        this.nadeout = true;
        this.itemid  = client.selectedweapon;
        this.mthrow  = 0.65;
        
        temp.muditem = new TStaticVar();
        temp.muditem.loadvarsfromarray(makevar("clientr.muditem_" @ this.itemid));
        
        this.curInfo = {temp.muditem.gani, temp.muditem.range, temp.muditem.type};
        
        onTimeout();
      }
        else
      {
        player.addMsg("Server", "You can only use one explosive every 2 seconds!", 15);
      }
    }
  }
}

function onTimeout()
{
  if (keydown(4))
  {
    if (this.nadeout == true && this.mtick <= 0)
    {
      setani("grab", NULL);
      playerfreeze = 0.05;
      
      if (this.mthrow < this.curInfo[1])
      {
        this.mtick = 0.05;
        this.mthrow += 0.05;
      }
    }
  }
    elseif (this.nadeout == true)
  {
    onThrowNade();
  }
   
  if (this.ttick > 0) this.ttick -= 0.05;
  if (this.mtick > 0) this.mtick -= 0.05;
  
  setTimer(0.05);
}

function onKeyPressed(keycode, keychar)
{
  if (keychar == "F" && this.nadeout == true)
  {
    this.mthrow = 0;
    
    onThrowNade();
  }
}

function onThrowNade()
{
  this.bounce = 0;
  
  if (this.ttick <= 0)
  {
    triggerserver("gui", name, "usenade", {this.itemid});

    playerfreeze = 0.45;
    this.ttick = 1;
    
    this.angle = getangle(vecx(player.dir), vecy(player.dir)), 1.57 / 2;
    
    if (player.guild != NULL)
    {
      setshootparams(player.account, 15, player.guild, client.nopk, false, this.curInfo[2], this.angle, this.mthrow);
    }
      else
    {
      setshootparams(player.account, 15, "(none)", client.nopk, false, this.curInfo[2], this.angle, this.mthrow); 
    }
    
    shoot(player.x, player.y, player.z, this.angle, 1.57 / 2, this.mthrow, this.curInfo[0], NULL);
  }
    else
  {
    player.addMsg("Server", "You can only throw one grenade every 2 seconds!", 15);
  }
  
  this.nadeout = false;
  this.mthrow = 0;
  
  setTimer(0.05);
}