//#CLIENTSIDE
function onCreated()
{
  if (CDPlayer_Window0 != NULL)
  {
    CDPlayer_Window0.destroy();
  }
}

function onWeaponFired()
{
  this.muditem = new TStaticVar();
  this.muditem.loadvarsfromarray(makevar("clientr.muditem_" @ client.selectedweapon));

  if (this.muditem.itemtype == "ARCHETYPE_TOY" && this.muditem.quantity >= 1 && this.muditem.weapon == name)
  {
    if (this.on == false)
    {
      if (this.muditem.battery >= 1)
      {
        if (this.muditem.songs.size() >= 0)
        {
          this.on = true;
          
          this.cdplayer = client.selectedweapon;
          
          this.cdids = player.Mud_FindArchetypeItems("ARCHETYPE_CD");
          
          onDisplayGui();
          
          onTimeout();
        }
          else
        {
          player.addMsg("Server", "You should get some songs first!", 15);
        }
      }
        else
      {
        player.addMsg("Server", "It seems the batteries are dead...", 15);
      }
    }
      else
    {
      stopmidi();
  
      CDPlayer_Window0.destroy();
      
      this.on    = false;
      
      setTimer(0);
    }
  }
}

function onDisplayGui()
{       
  this.muditem = new TStaticVar();
  this.muditem.loadvarsfromarray(makevar("clientr.muditem_" @ client.selectedweapon));
  
  new GuiWindowCtrl("CDPlayer_Window0")
  {
    extent   = {240, 180};
      
    profile = "GuiBlueWindowProfile";

    text = "CD Player - Battery: " @ thiso.muditem.battery @ "%";

    canresize = canmaximize = false;
    closequery = true;

    new GuiScrollCtrl("CDPlayer_Scroll")
    {
      extent     = {227, 150};
      position   = {7, 24};
      
      profile    = "GuiBlueScrollProfile";
      
      hscrollbar = "dynamic";
      vscrollbar = "dynamic";

      new GuiSliderCtrl("CDPlayer_Slider0")
      {
        extent   = {90, 20};
        position = {125, 15};
        
        profile  = "GuiBlueSliderProfile";
        
        range    = {0, 1};
        ticks    = 1;
        value    = 1;
      }

      new GuiButtonCtrl("CDPlayer_Button_Stop")
      {
        extent   = {50, 20};
        position = {65, 13};
        
        profile  = "GuiBlueButtonProfile";
        
        text     = "Stop";
  
        thiso.catchevent(name, "onAction", "onButtonEnter");
      }
      
      new GuiButtonCtrl("CDPlayer_Button_Play")
      {
        extent   = {50, 20};
        position = {8, 13};
        
        profile = "GuiBlueButtonProfile";
        
        text    = "Play";
        
        thiso.catchevent(name, "onAction", "onButtonEnter");
      }
      
      new GuiPopUpMenuCtrl("CDPlayer_CD_List")
      {
        extent         = {200, 20};
        position       = {12, 50};
        
        profile        = "GuiBluePopUpMenuProfile";
        scrollprofile  = "GuiBluePopUpMenuProfile";
       
        text           = "Choose CD";
        
        for (temp.var: thiso.cdids)
        {
          temp.md = new TStaticVar();
          temp.md.loadvarsfromarray(makevar("clientr.muditem_" @ temp.var));
        
          addrow(temp.I, temp.md.displayname);
          
          temp.I++;
        }
      }
      
      new GuiPopUpMenuCtrl("CDPlayer_Song_List")
      {
        extent         = {200, 20};
        position       = {12, 85};
        
        profile        = "GuiBluePopUpMenuProfile";
        scrollprofile  = "GuiBluePopUpMenuProfile";
       
        text           = "Choose Song";
      }
    }
  }
}

function onTimeout()
{
  if (this.on == true)
  {
    this.muditem = new TStaticVar();
    this.muditem.loadvarsfromarray(makevar("clientr.muditem_" @ this.cdplayer));
   
    /*
    this.ticker += 0.05; 

    if (this.ticker >= 5)
    {
      if (this.msong == true && musiclen == 0)
      {
        this.ticker = 0;
        this.scroll++;
        if (this.songurl[this.scroll][0] == "") this.scroll = 0;
        
        play(this.songurl[this.scroll][0]);
      } 
    }
    */
    
    if (this.muditem.quantity >= 1)
    {
      if (this.btick <= 0)
      {
        this.btick = 40;
        CDPlayer_Window0.text = "CD Player - Battery: " @ this.muditem.battery @ "%";

        triggerserver("gui", name, "battery", this.cdplayer);
      }
    }
      else
    {
      stopmidi();
      CDPlayer_Window0.destroy();
      this.on = false;
      this.msong = false;
    }
    
    if (this.btick > 0) this.btick -= 0.05;
    
    setTimer(0.05);
  }
}

function CDPlayer_Slider0.onAction(newValue)
{
  setmusicvolume(newValue, newValue);
}

function onButtonEnter(obj)
{
  if (obj == "CDPlayer_Button_Stop")
  {
    stopmidi();
    this.msong = false;
  }
    elseif (obj == "CDPlayer_Button_Play")
  {
    this.scroll = CDPlayer_Song_List.getselectedrow();
    play(this.songurl[this.scroll][0]);
    this.msong = true;
    this.ticker = 0;
    setmusicvolume(CDPlayer_Slider0.value, CDPlayer_Slider0.value);
  }
}

function CDPlayer_CD_List.onSelect(entryid, entrytext, entryindex)
{
  temp.muditem = new TStaticVar();
  temp.muditem.loadvarsfromarray(makevar("clientr.muditem_" @ this.cdids[entryid]));

  temp.server = {"www.graalx.net", 80, "/music/" @ temp.muditem.album @ "/tracks.txt"};
  temp.cmd    = requesthttp(temp.server[0], temp.server[1], temp.server[2]);
 
  catchevent(temp.cmd, "onReceiveData", "onGetInfo");
}

function onGetInfo(data)
{
  temp.toks = data.fulldata.tokenize("
");
  CDPlayer_Song_List.clearrows();
  this.songurl = "";
  
  for (temp.var: temp.toks)
  {
    temp.toks2 = temp.var.tokenize("|");
    
    this.songurl.add(temp.toks2);
    
    CDPlayer_Song_List.addrow(temp.I, temp.toks2[1]);
  }
}

function CDPlayer_Window0.onCloseQuery()
{
  stopmidi();
  
  CDPlayer_Window0.destroy();
  
  this.on = false;
  
  setTimer(0);
}

function onKeyPressed(keycode, keychar)
{
  if (keychar == "F")
  {
    temp.muditem = new TStaticVar();
    temp.muditem.loadvarsfromarray(makevar("clientr.muditem_" @ client.selectedweapon));
    
    if (temp.muditem.weapon == this.name)
    {
      triggerserver("gui", name, "refill", client.selectedweapon);
    
      player.addMsg("Server", "You have put new batteries in your CD Player!", 15);
    }
  }
}