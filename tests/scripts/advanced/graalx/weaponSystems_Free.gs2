//#CLIENTSIDE
function onCreated()
{
  requesttext("clientrc", 1);
  
  ScrollEditorProfile.destroy();
  
  new GuiControlProfile(TextEditorProfile)
  {
    fontType        = "Courier";
    fontSize        = 10;
    cursorColor     = "0 0 0";
    fillColorHL     = "255 255 0";
    fontColor       = "0 0 0";
    fontColorHL     = "192 0 0"; // control words like if, else etc.
    fontColorSEL    = "0 96 0"; // identifiers
    fontColorLink   = "0 0 192"; // strings
    fontColorLinkHL = "0 0 255"; // numbers
  }

  new GuiControlProfile(ScrollEditorProfile)
  {
    bitmap = "guiblue_scroll.png";
    
    opaque = true;
  }
  
  new GuiControlProfile(ScriptsWindowProfile)
  {
    bitmap = "guiblue_window.png";
    
    opaque = true;
  }
  
  onShowGUI();
}

function onShowGUI()
{ 
  if (Scripts_Window != NULL)
  {
    Scripts_Window.destroy();
  }
  
  new GuiWindowCtrl(Scripts_Window)
  {
    height               = 460;
    profile              = ScriptsWindowProfile;
    text                 = "KuJi's Public Script Library ";
    width                = 577;
    x                    = 150;
    y                    = 150;
    
    visible              = false;
    
    new GuiTabCtrl(Scripts_Tab)
    {
      profile = "GuiBlueTabProfile";
      extent = {380, 25};
      position = {183, 35};
      
      horizSizing = "width";
      clearrows();
    }
    
    new GuiScrollCtrl(Scripts_Frame1)
    {
      profile        = "ScrollEditorProfile";
      extent         = {175, 431};
      position       = {6, 23};
      
      hScrollBar     = "dynamic";
      vScrollBar     = "dynamic";
      vertSizing     = "height";
      
      new GuiTreeViewCtrl(Scripts_Tree)
      {
        profile = GuiBlueTreeViewProfile;
        x = y = 0;
        fitparentwidth = true;
       
        clearnodes();
        addnodebypath("Classes", "/");
        addnodebypath("NPCS", "/");
        addnodebypath("Weapons", "/");
      }
    }
    
    new GuiScrollCtrl(Scripts_Frame2)
    {
      profile        = "ScrollEditorProfile";
      extent         = {388, 355};
      position       = {182, 60};
      
      hScrollBar     = "dynamic";
      vScrollBar     = "dynamic";
      horizSizing    = "width";
      vertSizing     = "height";
    }
    
    new GuiButtonCtrl(Scripts_Close)
    {
      profile     = GuiBlueButtonProfile;
      extent      = {65, 25};
      position    = {360, 423};
      
      horizSizing = "left";
      vertSizing  = "top";
      text        = "Close";
      
      thiso.catchevent(this.name, "onAction", "onActionButton");
    }
    
    
    new GuiButtonCtrl(Scripts_Reload)
    {
      profile     = GuiBlueButtonProfile;
      extent      = {65, 25};
      position    = {430, 423};
      
      horizSizing = "left";
      vertSizing  = "top";
      
      text        = "Reload";
      
      thiso.catchevent(this.name, "onAction", "onActionButton");
    }
    
    new GuiButtonCtrl(Scripts_Save)
    {
      profile     = GuiBlueButtonProfile;
      extent      = {65, 25};
      position    = {500, 423};
      
      horizSizing = "left";
      vertSizing  = "top";
      
      text        = "Save";
      
      thiso.catchevent(this.name, "onAction", "onActionButton");
    }
  }
}

function Scripts_Tab.onSelect(tabid, tabtext, tabindex)
{
  for (temp.var: Scripts_Tab.rows)
  {
    if (temp.var.text == tabtext)
    {
      temp.var.scriptedit.visible = true;
    }
  }
}

function Scripts_Tab.onDeselect(tabid, tabtext, tabindex)
{
  for (temp.var: Scripts_Tab.rows)
  {
    if (temp.var.text == tabtext)
    {
      temp.var.scriptedit.visible = false;  
    }
  }
}


function Scripts_Tree.onDblClick(node, nodeslashpath, nodedotpath)
{
  if (nodeslashpath.starts("Classes/"))
  {
    requesttext("class", nodeslashpath.substring(8));
  }
    elseif (nodeslashpath.starts("Weapons/"))
  {
    requesttext("weapon", nodeslashpath.substring(8));
  }
    elseif (nodeslashpath.starts("NPCS/"))
  {
    requesttext("npc", nodeslashpath.substring(5));
  }
}

function onReceiveText(texttype, textoption, textlines)
{
  if (texttype in {"classlist", "npclist", "weaponlist"})
  {
    onAddScripts(textoption, textlines);
  }
    elseif (texttype in {"class", "npc", "weapon"})
  {
    onViewScript(texttype, textoption, textlines);
  }
}

function onAddScripts(temp.prefix, temp.textlines)
{
  if (temp.prefix != NULL)
  {
    for (temp.var: temp.textlines)
    {
      if (temp.var != NULL)
      {
        with (Scripts_Tree.addnodebypath(temp.prefix @ temp.var, "/"))
        {
          image = selectedimage = 2;
        }
      }
    }
  }
}

function onViewScript(temp.texttype, temp.textoption, temp.textlines)
{
  temp.found = Scripts_Tab.findtextid(temp.textoption);
  
  if (temp.found >= 0)
  {
    Scripts_Tab.setselectedbyid(temp.found);
    
    for (temp.var: Scripts_Tab.rows)
    {
      if (temp.var.id == temp.found)
      {
        temp.var.scriptedit.setlines(temp.textlines);
      }
    }
    
    return true;
  }
    else
  {
    
    with (Scripts_Frame2)
    {
      new GuiMLTextEditCtrl("Scripts_Edit_" @ temp.texttype @ "_" @ temp.textoption)
      {
        profile            = "TextEditorProfile";
        extent             = {365, 225};
        position           = {6, 6};
        
        horizSizing        = "width";
        vertSizing         = "height";
        
        syntaxHighlighting = true;
        wordwrap           = false;
      
        this.edittype      = temp.texttype;
        this.editoption    = temp.textoption;
        
        setlines(temp.textlines);
      }
    }
  
    with (Scripts_Tab.addrow(this.tabid++, temp.textoption))
    {
      this.scriptedit = makevar("Scripts_Edit_" @ temp.texttype @ "_" @ temp.textoption);
      Scripts_Tab.setselectedbyid(this.id);
    }
  } 
}

function onActionButton(obj)
{
  temp.ncid = Scripts_Tab.getselectedid();
  
  if (temp.ncid <= -1)
  {
    return false;
  }
  
  switch (obj.substring(3))
  {
    case "Close":
    {
      for (temp.var: Scripts_Tab.rows)
      {
        if (temp.var.id == temp.ncid)
        {
          temp.var.scriptedit.destroy();
          Scripts_Tab.removerowbyid(temp.ncid);
          Scripts_Frame2.extent = {388, 355};
          
          break;
        }
      }
    
      break;
    }
    
    case "Reload":
    {
      for (temp.var: Scripts_Tab.rows)
      {
        if (temp.var.id == temp.ncid)
        {
          requesttext(temp.var.scriptedit.edittype, temp.var.scriptedit.editoption);
          
          break;
        }
      }
      
      break;
    }
    
    case "Save":
    {
      for (temp.var: Scripts_Tab.rows)
      {
        if (temp.var.id == temp.ncid)
        {
          sendtext(temp.var.scriptedit.edittype, temp.var.scriptedit.editoption, temp.var.scriptedit.getLines());

          break;
        }
      }
    }
  }
}

function onKeyPressed(keycode, keychar)
{
  if (keycode == 308)
  {
    Scripts_Tree.clearnodes();
    requesttext("classlist", "Classes/");
    requesttext("npclist", "NPCS/");
    requesttext("weaponlist", "Weapons/");
    
    Scripts_Window.visible = !Scripts_Window.visible;
  }
}